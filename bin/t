#!/usr/bin/env bb

(ns t
  (:require [babashka.fs :as fs]
            [babashka.process :as p]
            [clojure.string :as string]))

(defn compress
  [dir]
  (let [data-dir (fs/path dir "_backups")
        get-year (fn [p] (->> (fs/file-name p)
                              (take-while (comp parse-long str))
                              (apply str)))
        files (->> (fs/list-dir data-dir)
                   (remove (fn [p] (= ".DS_Store" (fs/file-name p)))))]
    (fs/with-temp-dir [result-d]
      (doseq [[year files] (->> files
                                (group-by get-year)
                                (sort-by (fn [[y fs]] (parse-long y))))]
        (let [?year-file (->> files
                              (filter (fn [p] (= (format "%s.tar.zst" year)
                                                 (fs/file-name p))))
                              first)]
          (fs/with-temp-dir [year-d]
            (let [cp (fn [f] (fs/copy f (fs/path year-d (fs/file-name f))))
                  rm (fn [f] (fs/delete (fs/path year-d (fs/file-name f))))
                  tar-x (fn [f] (p/shell "tar" "xf" (fs/path year-d (fs/file-name f)) "-C" year-d))
                  tar-c (fn [] (p/shell "tar"
                                        "-I zstd -22 -M50 --ultra"
                                        "-f" (fs/path result-d (str year ".tar.zst"))
                                        "-C" year-d
                                        "-c" year))
                  mv (fn [f]
                       (let [dir-name (-> (fs/file-name f)
                                          (string/split #"\.")
                                          first)]
                         (fs/move (fs/path year-d dir-name) (fs/path year-d year))))]
              (if (and ?year-file
                       (= 1 (count files)))
                (fs/copy ?year-file (fs/path result-d (fs/file-name ?year-file)))
                (do (if ?year-file
                      (do (cp ?year-file)
                          (tar-x ?year-file)
                          (rm ?year-file))
                      (fs/create-dir (fs/path year-d year)))
                    (doseq [f (->> files (remove #{?year-file}))]
                      (cp f)
                      (tar-x f)
                      (rm f)
                      (mv f))
                    (tar-c)))))))
      (fs/move data-dir (fs/path dir "trash"))
      (fs/move result-d (fs/path dir "_backups"))
      (fs/delete-tree (fs/path dir "trash")))))

(defn -main
  [dir & args]
  (cond
    (= ["compress"] args) (compress dir)
    :else (do (println "Unrecognized command: ")
              (prn args)
              (System/exit 1))))

(if (= *file* (System/getProperty "babashka.file"))
  (apply -main
         (fs/parent (fs/parent *file*))
         *command-line-args*))
